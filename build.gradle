plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'signing'
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'ru.vyarus.quality' version '6.0.1'
    id 'com.github.spotbugs' version '6.4.5' apply false
    id 'net.researchgate.release' version '3.1.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'ru.vyarus.mkdocs' version '4.0.1'
}

java {
    sourceCompatibility = 11
}

wrapper {
    gradleVersion = '8.14.3'
}

ext {
    orient = '3.2.46'
    dropwizard = '3.0.16'
}

repositories { mavenLocal(); mavenCentral() }
dependencies {
    implementation platform("io.dropwizard:dropwizard-bom:$dropwizard")

    provided "com.orientechnologies:orientdb-studio:$orient"

    implementation 'io.dropwizard:dropwizard-core'
    implementation "com.orientechnologies:orientdb-server:$orient"
    implementation "com.orientechnologies:orientdb-tools:$orient"

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'ru.vyarus:spock-junit5:1.2.0'
    testImplementation "com.orientechnologies:orientdb-object:$orient"
    testImplementation 'io.dropwizard:dropwizard-testing'
    testImplementation 'org.spockframework:spock-core:2.4-M6-groovy-4.0'

    // used to catch System.exit statements
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.8'

    // check everything works together
    // lucene includes graph dependency
    testImplementation "com.orientechnologies:orientdb-lucene:$orient"
    testImplementation ("com.orientechnologies:orientdb-etl:$orient") {
        // ships with embedded slf4j-impl conflicting with dropwizard's logback
        exclude module: 'chromeinspector'
    }
    // OLDAPImporter by default is released only with Enterprise Edition, but technically itâ€™s part
    // of the security module, that is open-source, so you can add it to your CE distribution
    testImplementation "com.orientechnologies:orientdb-security:$orient"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

group = 'ru.vyarus'
description = 'Embedded OrientDB server for dropwizard'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs {
    publish {
        docPath = '3.0.0'
        rootRedirect = true
        rootRedirectTo = 'latest'
        versionAliases = ['latest']
    }
    extras = [
            'version': '3.0.0'
    ]
}

maven {
    pom {
        developers {
            developer {
                id = 'xvik'
                name = 'Vyacheslav Rusakov'
                email = 'vyarus@gmail.com'
            }
        }
    }
}

javaLib {
    // java 9 auto module name
    autoModuleName = "$project.group.$project.name"
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign).configureEach { onlyIf { !System.getenv('JITPACK') } }

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = ['publishToSonatype',
                 'closeAndReleaseSonatypeStagingRepository']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    // all tests use the same folder and so can't be run in parallel
    maxParallelForks = 1
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'

    jacoco {
        excludes = ['com.orientechnologies.*']
    }
    // appveyor fix
    systemProperty 'jna.nosys', 'true'
    // java > 16 does not allow SecurityManager by default (used to system.exit interception.. have to enable it)
    systemProperty 'java.security.manager', 'allow'
}

dependencyUpdates.revision = 'release'
